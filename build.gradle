/* use:
 * Download all dependencies - \programs\gradle-3.5\bin\gradle getTestRuntimeLibs
 * Download just the export dependencies -  \programs\gradle-3.5\bin\gradle getExportLibs
 */

apply plugin: 'java'

configurations {
    export {}
    compile { extendsFrom export }
}
dependencies {
    // public mendix runtime jars
    compile name: 'com.mendix.public-api'
    compile name: 'com.mendix.json'
    compile name: 'com.mendix.logging-api'
    compile name: 'com.mendix.m2ee-api'
    compile name: 'javax.servlet'
    // unit test module
    testRuntime 'junit:junit:4.11'
    testRuntime 'commons-io:commons-io:2.3'
    testRuntime 'org.apache.commons:commons-lang3:3.6'
    testRuntime 'org.apache.httpcomponents:httpclient:4.4.1'
    testRuntime 'org.apache.httpcomponents:httpcore:4.4.1'
    // for appcloudservices
    testRuntime 'org.openid4java:openid4java:1.0.0'
    testRuntime 'commons-lang:commons-lang:2.5'
    testRuntime 'org.apache.ws.commons.axiom:axiom-api:1.2.19'
    // export slack client sdk jars for upload to AppStore
    export 'com.ullink.slack:simpleslackapi:1.0.0'
}

repositories {
    mavenCentral()
    flatDir {
        dirs '/c://Program Files/Mendix/7.3.0/runtime/bundles'
    }
}

task getExportlibs(type: Copy) {
    doFirst {
        delete fileTree(dir: "userlib")
    }
    from configurations.export
    into 'userlib/'
    doLast {
        files { file('userlib').listFiles() }.each { File file ->
            println "file found: $file.name"
            def keepFilename = 'userlib/' + file.name + '.ConnectorKitDemo.RequiredLib'
            ant.touch(file: keepFilename)
        }
    }
}

task getTestRuntimelibs(type: Copy) {
    from configurations.testRuntime
    into 'userlib/'
}
